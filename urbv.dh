
include "std/io"
include "std/drw"
include "std/rand"
include "std/math"
include "std/time"


W := 800
H := 800


rand.seed(time.stamp())


drw.size(W,H);
drw.background(0.1,0.2,0.3);


cw : f32;
ch : f32;


wx : f32;
wy : f32;
ww : f32;
wh : f32;
nw : i32;
dw : i32;
br : i32;
pp : i32;


cc : vec[f32,3];


func draw_cell(){
  drw.no_stroke();
  bc := cc+rand.random({0.05,0.05,0.05});
  drw.fill(...bc);
  drw.rect(0,0,cw,ch);
  if (br){
    drw.stroke(...(cc-{0.1,0.1,0.1}));
    drw.stroke_weight(1);
    for (i := 0; i < 10; i++){
      drw.line(0,i*ch/10,cw,i*ch/10);
    }
    for (i := 0; i < 20; i++){
      drw.line(i*cw/20,0,i*cw/20,ch);
    }
  }



  drw.stroke(rand.random(0.7,1.0));
  drw.stroke_weight(5);
  drw.rect(wx,wy,ww,wh);
  drw.no_stroke();


  for (i := 0; i < nw; i++){
    bc := rand.random(0.5,0.6);
    if (rand.random()<0.2){
      bc *= 0.6;
    }
    drw.fill(...(bc+rand.random({0.1,0.1,0.1})));
    
    drw.rect(wx+i*(ww/nw),wy,ww/nw,wh);
  }


  np : i32 = rand.random(-12,4);
  for (k := 0; k < np; k++){
    drw.no_stroke();
    px := wx+rand.random(10.,ww-10)
    py := wy+wh-12;
    pc := rand.random({0.1,0.3,0.0},{0.2,0.4,0.1})
    fr := rand.random()*2-1.0;
    fc : vec[f32,3];


    fc = rand.random({0.7,0.2,0.2},{0.9,0.7,0.5})
    
    for (i := 0; i < 10; i++){
      drw.fill(...(pc+rand.random({0.1,0.1,0.1})));
      drw.push_matrix();
      drw.translate(px+rand.random(-6,6),py+rand.random(-5,8));
      drw.rotate_deg(rand.random(360.))
      drw.rect(-3,-3,6,6);
      drw.pop_matrix();
      if (i == 7){
        drw.fill(...rand.random({0.2,0.1,0.1},{0.5,0.2,0.2}));
        pw := rand.random(10,20);
        drw.rect(px-pw/2,py+5,pw,6);
      }
      if (rand.random()<fr){
        drw.fill(...(fc+rand.random({0.1,0.1,0.1})))
        drw.circle(px+rand.random(-6,6),py+rand.random(-6,6),3);
      }
    }
  }
  drw.stroke_weight(3);
  drw.no_fill();
  for (i := 0; i < nw; i++){
    drw.stroke(rand.random(0.8,1.0));
    drw.rect(wx+i*(ww/nw),wy,ww/nw,wh);
  }
  if (dw){
    drw.line(wx,wy+wh*0.4,wx+ww,wy+wh*0.4);
  }
  if (pp && ww < cw*0.7){
    ppx := math.lerp(wx+ww,cw,0.6)
    drw.stroke(0.6,0.55,0.5);
    drw.stroke_weight(5);
    drw.line(ppx,0,ppx,ch);
    drw.stroke(0.9);
    drw.stroke_weight(2.5);
    drw.line(ppx-0.25,0,ppx-0.25,ch-1);
  }
  if (rand.random() < 0.4){
    wcx := wx - 5;
    wcw := ww + 10;
    wcy := wy - 5;
    wch := wh + 10;
    drw.no_fill();
    drw.stroke_weight(2);
    drw.stroke(rand.random(0.3,0.6));
    drw.rect(wcx,wcy,wcw,wch);
    for (i := 0; i < wcw; i += 10){
      drw.line(wcx+i,wcy,wcx+i,wcy+wch);
    }
    drw.line(wcx,wcy+wch-5,wcx+wcw,wcy+wch-5);
    if (rand.random()<0.5){
      pct := rand.random(0.2,0.8);
      drw.line(wcx,wcy+wch*pct,wcx+wcw,wcy+wch*pct);
    }
    if (rand.random()<0.5){
      drw.line(wcx,wcy+5,wcx+wcw,wcy+5);
    }else{
      drw.fill(rand.random(0.2,0.5));
      drw.no_stroke();
      drw.rect(wcx-2,wcy-2,wcw+4,8);
    }


  }
  lsp := ch-(wy+wh);
  if (rand.random()<0.65 && lsp > 40){
    acx := rand.random(0.,cw-40);
    acy := ch-lsp+5
    drw.no_stroke();
    drw.fill(0.6);
    drw.rect(acx,acy,40,35);
    drw.fill(rand.random(0.85,0.95));
    drw.rect(acx,acy,40,25);
    if (rand.random()<0.3){
      drw.fill(0.5);
      drw.rect(acx+2,acy+2,22,22)
    }
    if (rand.random()<0.4){
      drw.fill(...rand.random({0.1,0.1,0.1},{0.9,0.9,0.9}));
      drw.rect(acx+28,acy+3,7,5);
    }
    drw.fill(0.3);
    drw.circle(acx+13,acy+12,20);
    drw.stroke(0.2,0.1,0.0);
    drw.no_fill();
    drw.stroke_weight(3);
    drw.begin_shape();
    drw.vertex(acx+4,acy+40);
    drw.vertex(acx+4,acy+30);
    drw.vertex(acx+36,acy+30);
    drw.vertex(acx+36,acy+40);
    drw.end_shape();
    
  }
  if (rand.random() < 0.5){
    drw.stroke(0.3,0.2,0.1);
    drw.no_fill();
    drw.stroke_weight(3);
    drw.begin_shape();
    drw.vertex(wx,wy+wh+5);
    drw.vertex(wx,wy+wh-10);
    drw.vertex(wx+ww,wy+wh-10);
    drw.vertex(wx+ww,wy+wh+5);
    drw.end_shape();
    n := math.floor(rand.random(7.));
    for (i := 1; i < n; i++){
      drw.stroke(0.4,0.3,0.2);
      x0 := wx+i/n*ww+rand.random(-5,5)
      x1 := wx+i/n*ww+rand.random(-5,5)
      drw.line(x0,wy+wh-15,x1,wy+wh+5);
      drw.no_stroke();


      m : = math.floor(rand.random(1,4));
      for (j := 0; j < m; j++){
        if (rand.random()<0.5){
          drw.fill(...rand.random({0.8,0.8,0.8},{1.0,1.0,1.0}));
        }else{
          drw.fill(...rand.random({0.2,0.2,0.2},{0.8,0.8,0.8}));
        }
        x := math.lerp(x1,x0,j/m);
        y := wy+wh+5-10*j/m;
        drw.begin_shape();
        drw.vertex(x-5,y-rand.random(0,10));
        drw.vertex(x+5,y-rand.random(0,10));
        drw.vertex(x+5,y+rand.random(10,30));
        drw.vertex(x,y+rand.random(10,30));
        drw.vertex(x-5,y+rand.random(10,30));
        drw.end_shape();
      }
    }
  }


}


x := 0;
y := 0;
while (x < W){
  y = 0;
  cw = rand.random(80,120);
  ch = rand.random(98,102);
  wx = rand.random(cw*0.1,cw*0.2);
  ww = cw-wx*2;
  wy = rand.random(ch*0.1,ch*0.3);
  wh = rand.random(ch*0.5,ch*0.7)-wy;


  nw = rand.random(2,5)
  dw = rand.random()<0.8


  cc = rand.random({0.8,0.7,0.6},{0.9,0.8,0.7})
  br = rand.random()<0.7
  pp = rand.random()<0.5


  while (y < H){
    drw.push_matrix();
    drw.translate(x,y);
    draw_cell();
    drw.pop_matrix();
    y += ch;
  }
  x += cw;
}



while (1){
  drw.poll();
}


