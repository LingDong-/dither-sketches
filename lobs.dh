include "std/io"
include "std/str"
include "std/list"
include "std/arr"
include "std/vec"
include "std/dict"
include "std/math"
include "std/frag"
include "std/rdr"
include "std/win"
include "std/time"
include "std/rand"
include "std/img"

W := 800
H := 800
context := win.init(W,H,win.CONTEXT_3D)
rdr.init(context);
frag.init(context);

func finish_mesh(mesh:rdr.Mesh,n:i32,m:i32){
  col := rand.random(0.0,1.0)
  for (i := 0; i < n; i++){
    for (j := 0; j < m; j++){
      a := mesh.vertices[i*m+j];
      b := (j < m-1) ? mesh.vertices[i*m+j+1] : (a+a-mesh.vertices[i*m+j-1]);
      c := (i < n-1) ? mesh.vertices[i*m+m+j] : (a+a-mesh.vertices[i*m-m+j]);
      d := (j > 0)   ? mesh.vertices[i*m+j-1] : (a+a-b);
      e := (i > 0)   ? mesh.vertices[i*m-m+j] : (a+a-c);

      nml := ((c-a).cross(b-a).dir() + 
              (d-a).cross(c-a).dir() + 
              (e-a).cross(d-a).dir() + 
              (b-a).cross(e-a).dir()
      ).dir();
      mesh.normals.push(nml);
      mesh.colors.push({col,1.0,1.0,1.0});
    }
  }
  for (i:=0; i<n-1; i++){
    for (j:=0; j<m-1; j++){
      mesh.indices.push(i*m+j);
      mesh.indices.push((i+1)*m+(j+1)%m);
      mesh.indices.push(i*m+(j+1)%m);
      mesh.indices.push(i*m+j);
      mesh.indices.push((i+1)*m+(j+1)%m);
      mesh.indices.push((i+1)*m+j);
    }
  }
}

func head():rdr.Mesh{
  mesh := rdr.Mesh{};
  m := 32;
  n := 32;
  for (i := 0; i < n; i++){
    t := i/((n-1) as f32);
    
    for (j := 0; j < m; j++){
      s := j/((m-1) as f32)
      w := (1.0-t**2.0)*(t**0.2)*0.4+0.6; 
      
      l0 := math.sin(s*math.PI)*0.3-0.2;
      l1 := math.sin(s*math.PI*7.0)*0.1+0.1;

      x := math.cos(s*math.PI)*w*1.1;
      y := t * 2.5 + (l0*(1-t)+l1*(t));
      z := math.sin(s*math.PI)*w;

      if (i % 4 == 0 && (j+(i/4)%2*2) % 4 == 0){
        x*=1.1;
        y*=1.1;
        z*=1.1;
      }
      if (i == n-2 && ((j==m*2/5) || (j == m*3/5))){
        x*=1.15;
        y*=1.15;
        z*=1.15;
      }
      mesh.vertices.push({x,y,z});
    }
  }
  finish_mesh(mesh,n,m);
  return mesh;
}

func body(bw:f32):rdr.Mesh{
  mesh := rdr.Mesh{};
  m := 32;
  n := 16;
  for (i := 0; i < n; i++){
    t := i/((n-1) as f32);
    
    for (j := 0; j < m; j++){
      s := j/((m-1) as f32)
      w0 := (1.0-t**2.0)*0.2+0.7;
      h0 := w0*0.8;
      w1 := (1.0-t**2.0)*0.7+0.2;
      h1 := w1*0.5;
      w := 0.0;
      h := 0.0;
      if (0.25 <= s && s < 0.75){
        w = w0;
        h = h0;
      }else{
        w = w0*(t)+w1*(1-t);
        h = h0*(t)+h1*(1-t);
      }
      l0 := math.sin(s*math.PI)*0.1;
      l1 := math.max(math.sin(s*math.PI)*0.5,1.0-math.sin(s*math.PI))*0.2;
      x := math.cos(s*math.PI)*w*bw;
      y := t * 1.0 - (l0*t+l1*(1-t));
      z := math.sin(s*math.PI)*h*bw;

      mesh.vertices.push({x,y,z});
    }
  }
  finish_mesh(mesh,n,m);
  return mesh;
}

func tail():rdr.Mesh{
  mesh := rdr.Mesh{};
  m := 32;
  n := 64;
  for (i := 0; i < n; i++){
    t := i/((n-1) as f32);
    
    for (j := 0; j < m; j++){
      s := j/((m-1) as f32)
      w := (1.0-(1-t)**2.0)*0.25+0.2;  
      l0 := 0.0;
      l1 := math.sqrt(1-(s*2-1)**2);

      x := math.cos(s*math.PI)*w;
      y := t * 0.8 + (l0*(1-t)+l1*(t))*0.5;
      z := math.sin(s*math.PI)*w*0.2;

      if (i % 4 == 0 && (j+(i/4)%2*2) % 4 == 0){
        z+=0.03*(1-t);
      }
      if (j%4 == 1){
        z -= 0.01*t;
      }else if (j%4 == 3){
        z += 0.01*t;
      }
      mesh.vertices.push({x,y,z});
    }
  }
  finish_mesh(mesh,n,m);
  return mesh;
}


func lega(dir:f32):rdr.Mesh{
  mesh := rdr.Mesh{};
  m := 32;
  n := 8;
  for (i := 0; i < n; i++){
    t := i/((n-1) as f32);
    
    th := dir*math.PI*0.5*(1-t)**4;
    costh := math.cos(th);
    sinth := math.sin(th);

    for (j := 0; j < m; j++){
      s := j/((m-1) as f32)
      w := (t**4*math.sin(t*math.PI))*0.1+0.15;
      x := math.cos(s*math.PI)*w;
      y := t * 1.0;
      z := math.sin(s*math.PI)*w*0.5;

      x0 := x - dir*w;
      y0 := y - y;
      x1 := x0* costh-y0*sinth;
      y1 := x0* sinth+y0*costh;
  
      mesh.vertices.push({x1+dir*w,y1+y,z});
    }
  }
  finish_mesh(mesh,n,m);
  return mesh;
}

func legb():rdr.Mesh{
  mesh := rdr.Mesh{};
  m := 32;
  n := 8;
  for (i := 0; i < n; i++){
    t := i/((n-1) as f32);
    
    for (j := 0; j < m; j++){
      s := j/((m-1) as f32)
      w := (t**4*math.sin(t*math.PI))*0.1+0.15;
      x := math.cos(s*math.PI)*w;
      y := t * 1.0;
      z := math.sin(s*math.PI)*w*0.5;
      mesh.vertices.push({x,y,z});
    }
  }
  finish_mesh(mesh,n,m);
  return mesh;
}

func legc(dir:f32):rdr.Mesh{
  mesh := rdr.Mesh{};
  m := 64;
  n := 32;
  for (i := 0; i < n; i++){
    t := i/((n-1) as f32);
    
    for (j := 0; j < m; j++){
      s := j/((m-1) as f32)
      w := (1-t)*0.12;
      x := math.cos(s*math.PI)*w+t*t*t*0.2*dir;
      y := t * 1.0;
      z := math.sin(s*math.PI)*w*0.5;
      if (i > 0 && i % 8 == 0 && (j+(i/8)%2*2) % 8 == 0){
        x+=math.cos(s*math.PI)*0.05+t*0.12*dir;
        y+=0.2;
        z+=math.sin(s*math.PI)*0.05;
      }

      mesh.vertices.push({x,y,z});
    }
  }

  finish_mesh(mesh,n,m);
  return mesh;
}

func antenna(dir:f32):rdr.Mesh{
  mesh := rdr.Mesh{};
  m := 16;
  n := 17;
  for (i := 0; i < n; i++){
    t := i/((n-1) as f32);
    
    th := dir*math.PI*0.5*(1-t)**4;
    costh := math.cos(th);
    sinth := math.sin(th);

    for (j := 0; j < m; j++){
      s := j/((m-1) as f32)
      w := (1-(1-t)**3)*0.1+0.15;
      
      l0 := 0.0;
      l1 := math.sin(s*math.PI*7.0)*0.02+0.02;

      x := math.cos(s*math.PI)*w*1.1;
      y := t * 1.0 + (l0*(1-t)+l1*(t));
      z := math.sin(s*math.PI)*w;

      if (i % 4 == 0 && (j+(i/4)%2*2) % 4 == 0){
        x+=math.cos(s*math.PI)*0.05;
        y+=0.1;
        z+=math.sin(s*math.PI)*0.05;
      }

      x0 := x - dir*w;
      y0 := y - y;
      x1 := x0* costh-y0*sinth;
      y1 := x0* sinth+y0*costh;
  
      mesh.vertices.push({x1+dir*w,y1+y,z});
    }
  }
  finish_mesh(mesh,n,m);

  return mesh
}


func antennb(dir:f32):rdr.Mesh{
  mesh := rdr.Mesh{};
  m := 16;
  n := 64;
  for (i := 0; i < n; i++){
    t := i/((n-1) as f32);
    
    for (k := 0; k < 2; k++){
      for (j := 0; j < m; j++){
        s := j/((m-1) as f32)
        w := (1-t)*0.08+0.02;
        x := math.cos(s*math.PI)*w+(t**5)*1.5*dir;
        y := t * 6.5;
        z := math.sin(s*math.PI)*w*0.5;
        mesh.vertices.push({x,y,z});
      }
    }
  }
  finish_mesh(mesh,n*2,m);
  for (i := 0; i < n*2; i++){
    c := ((i+1)/2)%2;
    for (j := 0; j < m; j++){
      mesh.colors[i*m+j]={c,1.0,1.0,1.0};
    }
  }
  return mesh
}


func antennc():rdr.Mesh{
  mesh := rdr.Mesh{};
  m := 32;
  n := 8;
  for (i := 0; i < n; i++){
    t := i/((n-1) as f32);
    
    for (j := 0; j < m; j++){
      s := j/((m-1) as f32)
      w := (t**4*math.sin(t*math.PI))*0.075+0.075;
      x := math.cos(s*math.PI)*w;
      y := t * 1.0;
      z := math.sin(s*math.PI)*w*0.5;
      mesh.vertices.push({x,y,z});
    }
  }
  finish_mesh(mesh,n,m);
  return mesh;
}

func antennd(dir:f32):rdr.Mesh{
  mesh := rdr.Mesh{};
  m := 8;
  n := 16;
  for (i := 0; i < n; i++){
    t := i/((n-1) as f32);
    
    for (k := 0; k < 2; k++){
      for (j := 0; j < m; j++){
        s := j/((m-1) as f32)
        w := (1-t)*0.02+0.02;
        x := math.cos(s*math.PI)*w+(t**4)*0.25*dir;
        y := t * 1.0;
        z := math.sin(s*math.PI)*w*0.5;
        mesh.vertices.push({x,y,z});
      }
    }
  }
  finish_mesh(mesh,n*2,m);
  for (i := 0; i < n*2; i++){
    c := ((i+1)/2)%2;
    for (j := 0; j < m; j++){
      mesh.colors[i*m+j]={c,1.0,1.0,1.0};
    }
  }
  return mesh
}

func nose():rdr.Mesh{
  mesh := rdr.Mesh{};
  m := 16;
  n := 16;
  for (i := 0; i < n; i++){
    t := i/((n-1) as f32);
    for (j := 0; j < m; j++){
      s := j/((m-1) as f32)
      w := (1.0-t**2.0)*0.2+0.2;
      
      l0 := 0.0;
      l1 := math.sin(s*math.PI*7.0)*0.1+0.1;

      x := math.cos(s*math.PI)*w;
      y := t * 0.7 + (l0*(1-t)+l1*(t));
      z := math.sin(s*math.PI)*w*0.8;

      if (i % 4 == 0 && (j+(i/4)%2*2) % 4 == 0){
        x*=1.1;
        y*=1.1;
        z*=1.5;
      }
      mesh.vertices.push({x,y,z});
    }
  }
  finish_mesh(mesh,n,m);
  return mesh;
}


func eye():rdr.Mesh{
  mesh := rdr.Mesh{};
  m := 16;
  n := 16;
  for (i := 0; i < n; i++){
    t := i/((n-1) as f32);
    
    for (j := 0; j < m; j++){
      s := j/((m-1) as f32)
      w := 0.11;
      if (t > 0.5){
        tt := (t-0.5)/0.5
        w = math.sqrt(1-tt*tt)*0.1+0.01;
      }
      
      x := math.cos(s*math.PI)*w;
      y := t * 0.4;
      z := math.sin(s*math.PI)*w;
      mesh.vertices.push({x,y,z});
    }
  }
  finish_mesh(mesh,n,m);
  for (i := n*3/4; i < n; i++){
    for (j := 0; j < m; j++){
      c := (i%2)*0.5+(j%2)*0.5;
      mesh.colors[i*m+j]={c,1.0,1.0,1.0};
    }
  }
  return mesh;
}



mesh_head := head();
mesh_body0 := body(1.0);
mesh_body1 := body(0.95);
mesh_body2 := body(0.9);
mesh_body3 := body(0.85);
mesh_body4 := body(0.8);
mesh_tail0 := tail();
mesh_tail1 := tail();
mesh_tail2 := tail();
mesh_tail3 := tail();
mesh_tail4 := tail();
mesh_lega0l := lega(1.0);
mesh_legb0l := legb();
mesh_lega1l := lega(1.0);
mesh_legb1l := legb();
mesh_legcl := legc(1.0);
mesh_lega0r := lega(-1.0);
mesh_legb0r := legb();
mesh_lega1r := lega(-1.0);
mesh_legb1r := legb();
mesh_legcr := legc(-1.0);
mesh_antennal := antenna(1.0);
mesh_antennar := antenna(-1.0);
mesh_antennbl := antennb(1.0);
mesh_antennbr := antennb(-1.0);
mesh_antennc0 := antennc();
mesh_antennc1 := antennc();
mesh_antenndl := antennd(1.0);
mesh_antenndr := antennd(-1.0);
mesh_nose := nose();
mesh_eye := eye();

func draw_leg(a0:rdr.Mesh,b0:rdr.Mesh,a1:rdr.Mesh,b1:rdr.Mesh,c:rdr.Mesh,pos:vec[f32,3],angs:vec[f32,5],lens:vec[f32,5]){
  a0.draw(rdr.mat.translate(...pos)@*rdr.mat.rotate_deg(rdr.AXIS_Z,angs[0]-90.0)@*rdr.mat.scale(1.0,lens[0],1.0));
  pos += {math.cos(angs[0]*math.PI/180.0)*(lens[0]-0.05),math.sin(angs[0]*math.PI/180.0)*(lens[0]-0.05),-0.01};
  b0.draw(rdr.mat.translate(...pos)@*rdr.mat.rotate_deg(rdr.AXIS_Z,angs[1]-90.0)@*rdr.mat.scale(1.0,lens[1],1.0));
  pos += {math.cos(angs[1]*math.PI/180.0)*(lens[1]+0.05),math.sin(angs[1]*math.PI/180.0)*(lens[1]+0.05),-0.01};
  a1.draw(rdr.mat.translate(...pos)@*rdr.mat.rotate_deg(rdr.AXIS_Z,angs[2]-90.0)@*rdr.mat.scale(1.0,lens[2],1.0));
  pos += {math.cos(angs[2]*math.PI/180.0)*(lens[2]-0.05),math.sin(angs[2]*math.PI/180.0)*(lens[2]-0.05),-0.01};
  b1.draw(rdr.mat.translate(...pos)@*rdr.mat.rotate_deg(rdr.AXIS_Z,angs[3]-90.0)@*rdr.mat.scale(1.0,lens[3],1.0));
  pos += {math.cos(angs[3]*math.PI/180.0)*(lens[3]-0.01),math.sin(angs[3]*math.PI/180.0)*(lens[3]-0.01),-0.01};
  c.draw(rdr.mat.translate(...pos)@*rdr.mat.rotate_deg(rdr.AXIS_Z,angs[4]-90.0)@*rdr.mat.scale(1.0,lens[4],1.0));
  
}

func draw_antenna(a:rdr.Mesh,b:rdr.Mesh,pos:vec[f32,3],angs:vec[f32,4],lens:vec[f32,4]){
  a.draw(rdr.mat.translate(...pos)@*rdr.mat.rotate_deg(rdr.AXIS_Z,angs[0]-90.0)@*rdr.mat.scale(1.0,lens[0],1.0));
  pos += {math.cos(angs[0]*math.PI/180.0)*(lens[0]+0.05),math.sin(angs[0]*math.PI/180.0)*(lens[0]+0.05),-0.01};
  a.draw(rdr.mat.translate(...pos)@*rdr.mat.rotate_deg(rdr.AXIS_Z,angs[1]-90.0)@*rdr.mat.scale(1.0,lens[1],1.0));
  pos += {math.cos(angs[1]*math.PI/180.0)*(lens[1]+0.05),math.sin(angs[1]*math.PI/180.0)*(lens[1]+0.05),-0.01};
  a.draw(rdr.mat.translate(...pos)@*rdr.mat.rotate_deg(rdr.AXIS_Z,angs[2]-90.0)@*rdr.mat.scale(0.8,lens[2],0.8));
  pos += {math.cos(angs[2]*math.PI/180.0)*(lens[2]-0.05),math.sin(angs[2]*math.PI/180.0)*(lens[2]-0.05),-0.01};
  b.draw(rdr.mat.translate(...pos)@*rdr.mat.rotate_deg(rdr.AXIS_Z,angs[3]-90.0)@*rdr.mat.scale(1.0,lens[3],1.0));
}

func draw_minitenna(a:rdr.Mesh,b:rdr.Mesh,c:rdr.Mesh,pos:vec[f32,3],angs:vec[f32,4],lens:vec[f32,4]){
  a.draw(rdr.mat.translate(...pos)@*rdr.mat.rotate_deg(rdr.AXIS_Z,angs[0]-90.0)@*rdr.mat.scale(1.0,lens[0],1.0));
  pos += {math.cos(angs[0]*math.PI/180.0)*(lens[0]-0.05),math.sin(angs[0]*math.PI/180.0)*(lens[0]-0.05),-0.01};
  b.draw(rdr.mat.translate(...pos)@*rdr.mat.rotate_deg(rdr.AXIS_Z,angs[1]-90.0)@*rdr.mat.scale(1.0,lens[1],1.0));
  pos += {math.cos(angs[1]*math.PI/180.0)*(lens[1]-0.05),math.sin(angs[1]*math.PI/180.0)*(lens[1]-0.05),-0.01};
  a.draw(rdr.mat.translate(...pos)@*rdr.mat.rotate_deg(rdr.AXIS_Z,angs[2]-90.0)@*rdr.mat.scale(0.8,lens[2],0.8));
  pos += {math.cos(angs[2]*math.PI/180.0)*(lens[2]-0.05),math.sin(angs[2]*math.PI/180.0)*(lens[2]-0.05),-0.01};
  c.draw(rdr.mat.translate(...pos)@*rdr.mat.rotate_deg(rdr.AXIS_Z,angs[3]-85.0)@*rdr.mat.scale(1.0,lens[3],1.0));
  c.draw(rdr.mat.translate(...pos)@*rdr.mat.rotate_deg(rdr.AXIS_Z,angs[3]-95.0)@*rdr.mat.scale(1.0,lens[3],1.0));
}

func draw_scene(){
  mesh_head.draw(rdr.mat.id);
  mesh_nose.draw(rdr.mat.translate(0,2.3,0.0));
  mesh_eye.draw(rdr.mat.translate(0.2,2.5,0.5)@*rdr.mat.rotate_deg(rdr.AXIS_Z,-45));
  mesh_eye.draw(rdr.mat.translate(-0.2,2.5,0.5)@*rdr.mat.rotate_deg(rdr.AXIS_Z,45));

  mesh_body0.draw(rdr.mat.translate(0,-0.6,0.1)@*rdr.mat.rotate_deg(rdr.AXIS_X,-20));
  mesh_body1.draw(rdr.mat.translate(0,-1.1,0.2)@*rdr.mat.rotate_deg(rdr.AXIS_X,-10));
  mesh_body2.draw(rdr.mat.translate(0,-1.6,0.2)@*rdr.mat.rotate_deg(rdr.AXIS_X,0));
  mesh_body3.draw(rdr.mat.translate(0,-2.1,0.1)@*rdr.mat.rotate_deg(rdr.AXIS_X,10));
  mesh_body4.draw(rdr.mat.translate(0,-2.6,0.0)@*rdr.mat.rotate_deg(rdr.AXIS_X,15));

  mesh_tail0.draw(rdr.mat.translate(-0.2,-2.6,0.0)@*rdr.mat.rotate_deg(rdr.AXIS_Z,150));
  mesh_tail1.draw(rdr.mat.translate(-0.1,-2.6,0.2)@*rdr.mat.rotate_deg(rdr.AXIS_Z,165));
  mesh_tail2.draw(rdr.mat.translate(0,   -2.6,0.4)@*rdr.mat.rotate_deg(rdr.AXIS_Z,180));
  mesh_tail3.draw(rdr.mat.translate(0.1,-2.6,0.2)@*rdr.mat.rotate_deg(rdr.AXIS_Z,195));
  mesh_tail4.draw(rdr.mat.translate(0.2,-2.6,0.0)@*rdr.mat.rotate_deg(rdr.AXIS_Z,210));

  draw_antenna(mesh_antennal,mesh_antennbl,{0.25,2.4,0.15},{70,30,-20,-70},{0.6,0.6,0.6,1.0});
  draw_antenna(mesh_antennar,mesh_antennbr,{-0.25,2.4,0.15},{180-70,180-30,180+20,180+70},{0.6,0.6,0.6,1.0});

  draw_minitenna(mesh_antennc0,mesh_antennc1,mesh_antenndl,{0.075,2.9,0.0},{85,70,45,25},{0.6,0.4,0.4,1.5})
  draw_minitenna(mesh_antennc0,mesh_antennc1,mesh_antenndr,{-0.075,2.9,0.0},{180-85,180-70,180-45,180-25},{0.6,0.4,0.4,1.5})

  draw_leg(mesh_lega0l,mesh_legb0l,mesh_lega1l,mesh_legb1l, mesh_legcl,
    {0.3,0.0,-0.}, {0.,20,-40,-45,-50}, {0.7,0.8,0.7,0.8,0.5}
  );
  draw_leg(mesh_lega0l,mesh_legb0l,mesh_lega1l,mesh_legb1l, mesh_legcl,
    {0.4,0.4,-0.2}, {0.,25,-35,-50,-60}, {0.7,1.0,0.7,1.0,0.8}
  );
  draw_leg(mesh_lega0l,mesh_legb0l,mesh_lega1l,mesh_legb1l, mesh_legcl,
    {0.45,0.8,-0.2}, {5.,30,-10,-45,-60}, {0.7,1.0,0.8,1.0,0.8}
  );
  draw_leg(mesh_lega0l,mesh_legb0l,mesh_lega1l,mesh_legb1l, mesh_legcl,
    {0.4,1.2,-0.2}, {10.,35,0,-10,-40}, {0.7,1.0,0.7,0.8,0.7}
  );
  draw_leg(mesh_lega0l,mesh_legb0l,mesh_lega1l,mesh_legb1l, mesh_legcl,
    {0.3,1.6,-0.2}, {15.,45,30,20,10}, {0.7,0.8,0.5,0.6,0.6}
  );

  draw_leg(mesh_lega0r,mesh_legb0r,mesh_lega1r,mesh_legb1r, mesh_legcr,
    {-0.3,0.0,-0.2}, {180,180-20,180+40,180+45,180+50}, {0.7,0.8,0.7,0.8,0.5}
  );
  draw_leg(mesh_lega0r,mesh_legb0r,mesh_lega1r,mesh_legb1r, mesh_legcr,
    {-0.4,0.4,-0.2}, {180,180-25,180+35,180+50,180+60}, {0.7,1.0,0.7,1.0,0.8}
  );
  draw_leg(mesh_lega0r,mesh_legb0r,mesh_lega1r,mesh_legb1r, mesh_legcr,
    {-0.45,0.8,-0.2}, {180-5.,180-30,180+10,180+45,180+60}, {0.7,1.0,0.8,1.0,0.8}
  );
  draw_leg(mesh_lega0r,mesh_legb0r,mesh_lega1r,mesh_legb1r, mesh_legcr,
    {-0.4,1.2,-0.2}, {180-10.,180-35,180,180+10,180+40}, {0.7,1.0,0.7,0.8,0.7}
  );
  draw_leg(mesh_lega0r,mesh_legb0r,mesh_lega1r,mesh_legb1r, mesh_legcr,
    {-0.3,1.6,-0.2}, {180-15.,180-45,180-30,180-20,180-10}, {0.7,0.8,0.5,0.6,0.6}
  );
}


shader0 := frag.program(embed (func(
  @builtin frag_coord:vec[f32,4],
  @varying normal:vec[f32,3],
  @varying uv:vec[f32,2],
):vec[f32,4]{
  c := normal*0.5+0.5;
  return {...c,1.0};
}) as "fragment");

shader1 := frag.program(embed (func(
  @builtin frag_coord:vec[f32,4],
  @varying normal:vec[f32,3],
  @varying color:vec[f32,4],
  @varying uv:vec[f32,2],
):vec[f32,4]{
  c := frag_coord.w;
  return {c,...color.xy,1.0};
}) as "fragment");


// ord_dith_map := arr[i32,2]{
//    0,48,12,60, 3,51,15,63; 32,16,44,28,35,19,47,31;
//    8,56, 4,52,11,59, 7,55; 40,24,36,20,43,27,39,23;
//    2,50,14,62, 1,49,13,61; 34,18,46,30,33,17,45,29;
//   10,58, 6,54, 9,57, 5,53; 42,26,38,22,41,25,37,21;
// };

// ord_dith_map := arr[i32,2]{
//   0, 128, 32, 160, 8, 136, 40, 168, 2, 130, 34, 162, 10, 138, 42, 170;
//   192, 64, 224, 96, 200, 72, 232, 104, 194, 66, 226, 98, 202, 74, 234, 106;
//   48, 176, 16, 144, 56, 184, 24, 152, 50, 178, 18, 146, 58, 186, 26, 154;
//   240, 112, 208, 80, 248, 120, 216, 88, 242, 114, 210, 82, 250, 122, 218, 90;
//   12, 140, 44, 172, 4, 132, 36, 164, 14, 142, 46, 174, 6, 134, 38, 166;
//   204, 76, 236, 108, 196, 68, 228, 100, 206, 78, 238, 110, 198, 70, 230, 102;
//   60, 188, 28, 156, 52, 180, 20, 148, 62, 190, 30, 158, 54, 182, 22, 150;
//   252, 124, 220, 92, 244, 116, 212, 84, 254, 126, 222, 94, 246, 118, 214, 86; 
//   3, 131, 35, 163, 11, 139, 43, 171, 1, 129, 33, 161, 9, 137, 41, 169;
//   195, 67, 227, 99, 203, 75, 235, 107, 193, 65, 225, 97, 201, 73, 233, 105;
//   51, 179, 19, 147, 59, 187, 27, 155, 49, 177, 17, 145, 57, 185, 25, 153;
//   243, 115, 211, 83, 251, 123, 219, 91, 241, 113, 209, 81, 249, 121, 217, 89;
//   15, 143, 47, 175, 7, 135, 39, 167, 13, 141, 45, 173, 5, 133, 37, 165;
//   207, 79, 239, 111, 199, 71, 231, 103, 205, 77, 237, 109, 197, 69, 229, 101;
//   63, 191, 31, 159, 55, 183, 23, 151, 61, 189, 29, 157, 53, 181, 21, 149;
//   255, 127, 223, 95, 247, 119, 215, 87, 253, 125, 221, 93, 245, 117, 213, 85 
// };

// ord_dith_map := arr[i32,2]{
//   15,108,237,150,6,223,148,252,209,3,38,216,238,94,120,245;
//   145,192,26,210,60,124,30,88,61,193,83,158,142,64,32,163;
//   208,56,121,167,102,233,197,136,166,109,244,55,10,196,226,73;
//   2,90,250,39,79,178,48,12,225,26,132,213,119,170,103,131;
//   221,174,140,200,8,151,240,115,77,175,46,186,87,28,253,44;
//   99,68,23,230,126,96,65,195,141,246,100,14,236,62,154,187;
//   146,242,112,53,181,218,18,160,29,58,219,157,134,205,78,19;
//   215,34,194,162,80,37,254,107,206,127,81,191,35,105,231,122;
//   50,89,133,21,207,147,92,173,45,228,9,116,51,168,5,177;
//   155,203,249,69,114,232,1,137,74,179,149,251,202,91,241,71;
//   27,106,11,169,41,182,63,199,243,20,97,67,138,24,118,217;
//   143,185,235,125,86,212,128,33,111,165,211,40,224,161,189,43;
//   98,76,57,220,152,13,239,82,144,54,188,123,7,85,59,255;
//   4,198,164,22,104,52,159,190,222,25,93,248,172,227,110,156;
//   129,229,36,135,247,204,117,16,70,234,153,47,72,139,31,214;
//   84,176,66,95,183,75,42,171,101,130,184,113,17,201,180,49;
// };

ord_dith_map := arr[i32,2]{
  29,397,594,195,111,257,405,845,350,520,236,764,633,843,98,517,684,406,608,293,35,439,862,662,417,580,365,227,124,848,188,625;
  244,792,1015,469,861,703,548,75,993,710,121,894,28,427,945,178,784,63,856,502,985,571,225,84,916,165,987,729,503,322,418,924;
  699,103,341,641,27,308,941,161,461,271,616,376,552,295,721,621,330,964,247,163,722,321,776,500,713,288,450,22,896,648,87,559;
  840,495,168,763,913,438,656,787,585,828,951,184,790,1017,228,41,453,555,680,415,835,102,396,1013,44,637,820,554,246,770,998,294;
  394,944,582,253,530,122,222,335,8,401,85,694,456,117,532,803,927,133,871,10,611,934,197,588,853,368,190,966,385,143,472,7;
  214,654,65,819,362,969,854,716,1002,525,250,909,307,646,886,383,267,739,347,519,289,674,463,273,142,526,760,71,698,600,872,725;
  988,301,903,423,629,50,567,457,182,670,841,570,21,754,172,490,79,578,979,180,892,58,798,961,689,907,304,489,928,316,186,539;
  758,484,115,704,194,765,290,100,796,314,126,426,978,351,604,953,692,218,808,436,708,367,546,105,413,13,619,220,95,794,435,48;
  622,167,865,329,1018,505,879,386,601,963,501,705,224,831,51,287,844,398,31,607,144,990,240,634,777,343,825,1019,668,564,895,358;
  230,805,561,441,24,229,665,939,36,215,875,73,382,667,467,557,129,1007,499,755,318,811,487,176,937,524,139,454,375,265,119,965;
  675,380,91,950,612,826,123,465,731,338,553,761,1014,147,901,779,334,661,258,926,112,420,890,733,56,277,706,863,30,744,832,509;
  12,910,718,274,752,364,547,268,847,643,160,434,296,618,249,89,428,870,174,538,639,40,291,584,402,983,549,198,949,592,159,313;
  458,598,177,508,104,885,183,999,399,88,954,823,20,504,745,971,577,9,736,366,833,1000,687,204,789,81,653,331,497,404,688,1009;
  237,874,353,980,424,696,597,6,771,522,232,575,702,920,323,205,682,486,956,234,444,137,512,326,917,448,815,114,851,221,76,769;
  543,66,624,813,47,261,914,478,663,299,880,371,181,425,78,855,379,146,801,61,614,746,849,15,627,164,275,997,717,610,918,369;
  838,732,300,156,537,788,342,207,976,128,727,53,995,636,781,518,1008,282,642,352,900,259,387,967,498,759,579,421,18,302,477,113;
  193,431,943,685,1005,446,113,837,390,590,455,809,493,255,140,603,34,720,931,474,90,573,187,673,107,349,938,206,533,786,975,657;
  878,514,16,384,200,644,556,709,32,911,192,317,893,690,355,822,226,407,545,170,830,1020,433,785,264,859,64,707,906,155,400,262;
  574,134,904,286,772,83,929,266,513,780,650,99,562,42,984,462,884,120,795,272,697,319,38,942,602,485,647,408,284,606,39,743;
  327,817,677,589,412,857,333,157,1016,361,238,960,392,751,175,534,310,635,972,54,617,482,741,148,374,216,1003,130,802,867,481,1021;
  69,460,231,991,46,492,723,620,442,118,735,470,866,283,660,933,5,737,447,348,925,211,883,560,824,4,734,563,345,86,693,209;
  377,757,536,150,827,212,957,11,873,550,836,52,596,96,411,778,252,572,189,767,529,94,403,276,659,915,459,245,968,430,551,932;
  645,108,947,354,658,566,297,391,676,166,312,700,217,1010,511,145,877,388,1001,131,829,678,982,67,510,332,110,791,672,173,279,834;
  491,881,256,719,419,116,902,799,243,989,506,891,357,631,818,320,695,62,632,305,466,223,569,749,869,191,940,609,26,897,738,49;
  162,337,593,59,970,753,473,68,613,429,33,768,152,449,23,955,541,440,912,728,17,858,324,151,437,701,381,527,298,471,370,615;
  936,793,452,846,158,280,540,203,730,935,270,568,977,679,285,201,852,109,248,523,962,393,638,1012,37,814,233,974,138,762,1011,241;
  521,3,210,628,372,671,1022,839,344,136,669,395,92,800,494,740,595,360,807,153,605,80,782,278,496,581,82,652,850,558,74,686;
  359,747,992,488,905,43,410,97,586,868,475,921,196,340,882,72,416,1023,683,311,898,483,185,681,930,346,889,451,328,213,414,888;
  254,576,106,303,775,235,714,515,773,251,45,724,531,623,959,169,651,219,25,432,750,263,842,409,127,774,179,711,19,816,640,125;
  958,422,664,876,149,599,952,309,154,981,378,821,269,2,445,306,766,864,542,948,93,591,996,1,630,535,292,1004,587,919,479,756;
  171,810,55,528,356,464,14,887,443,655,565,132,1006,691,804,583,77,476,336,715,208,373,516,742,242,946,468,101,389,260,70,339;
  507,899,281,726,973,797,626,202,748,60,923,325,480,199,363,908,239,994,135,812,649,922,141,315,806,57,860,666,783,544,986,712;
}


shader2 := frag.program(embed(func(
  @builtin frag_coord:vec[f32,4],
  @uniform l_dir:vec[f32,3],
  @varying uv:vec[f32,2],
  @uniform image0:frag.Texture,
  @uniform image1:frag.Texture,
  @uniform cview:vec[f32,4,4],
  @varying normal:vec[f32,3]
):vec[f32,4]{
  u := {1.0/(W as f32), 1.0/(H as f32)};
  c0 := image0.sample(uv+u*{-1.0,-1.0}).xyz;
  c1 := image0.sample(uv+u*{0.0,-1.0}).xyz;
  c2 := image0.sample(uv+u*{1.0,-1.0}).xyz;
  c3 := image0.sample(uv+u*{-1.0,0.0}).xyz;
  c4 := image0.sample(uv+u*{0.0,0.0}).xyz;
  c5 := image0.sample(uv+u*{1.0,0.0}).xyz;
  c6 := image0.sample(uv+u*{-1.0,1.0}).xyz;
  c7 := image0.sample(uv+u*{0.0,1.0}).xyz;
  c8 := image0.sample(uv+u*{1.0,1.0}).xyz;

  d0 := image1.sample(uv+u*{-1.0,-1.0}).xyx;
  d1 := image1.sample(uv+u*{0.0,-1.0}).xyx;
  d2 := image1.sample(uv+u*{1.0,-1.0}).xyx;
  d3 := image1.sample(uv+u*{-1.0,0.0}).xyx;
  d4 := image1.sample(uv+u*{0.0,0.0}).xyx;
  d5 := image1.sample(uv+u*{1.0,0.0}).xyx;
  d6 := image1.sample(uv+u*{-1.0,1.0}).xyx;
  d7 := image1.sample(uv+u*{0.0,1.0}).xyx;
  d8 := image1.sample(uv+u*{1.0,1.0}).xyx;

  gx := -1.0*c0 + c2 - 2.0*c3 + 2.0*c5 - c6 + c8;
  gy := -1.0*c0 + c6 - 2.0*c1 + 2.0*c7 - c2 + c8;

  hx := -1.0*d0 + d2 - 2.0*d3 + 2.0*d5 - d6 + d8;
  hy := -1.0*d0 + d6 - 2.0*d1 + 2.0*d7 - d2 + d8;

  gx *= 1.0;
  gy *= 1.0;
  hx *= 10.0;
  hy *= 10.0;

  g := math.sqrt(gx*gx+gy*gy+hx*hx+hy*hy)*1.0;
  gg := math.sqrt(g.x*g.x+g.y*g.y+g.z*g.z);
  
  c := 1.0;
  if (gg>0.5){
    c = 0.0;
  }
  p := (frag_coord.xy as vec[i32,2])/2;
  // d := (ord_dith_map[p.y%16,p.x%16] as f32)/256.0;
  d := (ord_dith_map[p.y%32,p.x%32] as f32)/1024.0;
  nml := ((c0-0.5)*2.0);
  s := nml.dot(l_dir);

  if (s < d){
    c = 0.0;
  }
  r := 1.-(uv-{0.5,0.5}).mag();
  ct := {0.,0.,0.} * (1.-c) + ({1.0,0.95,0.85} * (1.-r) + {1.0,1.0,1.0} * r) * c;
  return {...ct,1.0};
}) as "fragment");

image0 := frag.texture(W,H,frag.WRAP_CLAMP);
image1 := frag.texture(W,H,frag.WRAP_CLAMP);

cam := rdr.Camera{};
cam.perspective(60,W/(H as f32),0.01,100.0);

yaw := 0.0//math.PI/6;
pitch := 0.0;//math.PI/12;
zoom := 8.0;
drag := {0.,0.,0.};
targ := {0.,0.,0.};
keys_held := dict[i32,i32]{};

l_dir := {1.0,2.0,3.0}.dir();

while (1){
  time.fps(120);
  // rdr.background(1.0,0.95,0.85);

  eye := {
    math.cos(pitch)*math.sin(yaw),
    math.sin(pitch),
    math.cos(pitch)*math.cos(yaw)
  }*zoom;
  horz := eye.cross({0,1.,0}).dir();
  vert := eye.cross(horz).dir();
  cam.look_at(eye+targ,targ,rdr.AXIS_Y);


  frag.begin(shader0,image0);
  rdr.background(1.0);
  cam.begin();
  draw_scene();
  cam.end();
  frag.end();

  frag.begin(shader1,image1);
  rdr.background(1.0);
  cam.begin();
  draw_scene();
  cam.end();
  frag.end();

  frag.begin(shader2);
  frag.uniform("l_dir",l_dir);
  frag.uniform("image0",image0);
  frag.uniform("image1",image1);
  frag.uniform("cview",cam.view);
  frag.render();
  frag.end();

  e := win.poll();
  if (e.type == win.MOUSE_PRESSED){
    drag = {e.x,e.y,(e.key==win.MOUSE_RIGHT || keys_held[win.KEY_LSHIFT as i32])?-1.0:1.0};
  }else if (e.type == win.MOUSE_RELEASED){
    drag = {e.x,e.y,0.0};
  }else if (e.type == win.MOUSE_MOVED){
    if (drag.z){
      dx := drag.x - e.x;
      dy := drag.y - e.y;
      if (drag.z > 0){
        yaw -= dx*0.01;
        pitch -= dy*0.01;
        pitch = math.min(math.max(pitch,-math.PI/2+0.01),math.PI/2-0.01);
      }else{
        targ -= horz * dx*0.005*zoom - vert * dy*0.005*zoom;
      }
      drag = {e.x,e.y,drag.z};
    }
  }else if (e.type == win.WHEEL_SCROLLED){
    zoom += e.y*0.01;
    if (zoom < 0.1){
      zoom = 0.1;
    }else if (zoom > 100.0){
      zoom = 100.0;
    }
  }else if (e.type == win.KEY_PRESSED){
    keys_held[e.key] = 1;
  }else if (e.type == win.KEY_RELEASED){
    keys_held[e.key] = 0;
  }
}
